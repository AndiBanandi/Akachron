#include "/script/Akachron/lib/include_system.txt"



float _humes = 0.9; //unit of realness - 1.0 is "baseline", currently defaults to 0.5 for testing purposes
int _numActiveEffects = 0;
int _objShader;

@Initialize
{
	_Main();
}

@MainLoop
{
	yield;
}

@Event
{
	//TODO: Events to manually trigger a specific effect
	//and probably an event to set the hume level because using common data for that is probably teeeeeechnically jank?
}

task _Main()
{
	loop
	{
		_humes = GetCommonData("HUME_LEVEL", _humes);
		
		if(_humes < 1.0 & rand(-0.01, _humes * (_numActiveEffects + 0.1)) < 0.1)
		{
			_DeadRegion(_humes);
		}
		
		yield;
	}
}

task _DeadRegion(float thresh_)
{	
	string renderTarg = "RENDERTARGET_SCREENSHOT" ~ ToString(rand_int(0, 9999));
	CreateRenderTarget(renderTarg);
	
	const int lower = 20;
	const int upper = 80;
	const int objPri = 90;
	
	SetInvalidRenderPriorityA1(lower, upper);
	RenderToTextureA1(renderTarg, lower, upper, false);
	ClearInvalidRenderPriority();
	
	yield;
	
	int prim = ObjPrim_Create(OBJ_PRIMITIVE_2D);
	ObjPrim_SetPrimitiveType(prim, PRIMITIVE_TRIANGLELIST);
	ObjPrim_SetVertexCount(prim, 3);
	ObjPrim_SetTexture(prim, renderTarg);
	Obj_SetRenderPriorityI(prim, objPri);
	Obj_SetRenderPriorityI(prim, 90);
	
	float centerX = rand(0, GetScreenWidth());
	float centerY = rand(0, GetScreenHeight());
	
	float offsetX = rand(-2, 2);
	float offsetY = rand(-2, 2);

	
	float[] xVerts = [];
	float[] yVerts = [];
	ascent(i in 0..3)
	{
		float th = rand(i * 120, (i+1) * 120);
		float r = rand(10, 100);
		
		xVerts = xVerts ~ [ centerX + r * cos(th) ];
		yVerts = yVerts ~ [ centerY + r * sin(th) ];
		
		ObjPrim_SetVertexPosition(prim, i, xVerts[i] + offsetX, yVerts[i] + offsetY, 0);
		ObjPrim_SetVertexUVT(prim, i, xVerts[i], yVerts[i]);
	}
	
	
	_numActiveEffects++;
	
	while(_humes <= thresh_ && (rand(0, 1000) > 1))
	{
		//ObjRender_SetPosition(prim, rand(-1, 1), rand(-1, 1), 0);
		yield;
	}
	
	Obj_Delete(prim);
	ClearRenderTargetA1(renderTarg);
	_numActiveEffects--;
}