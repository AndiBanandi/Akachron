func<int> CreateCutin(string title_)
{
	int objText = ObjText_Create();
	ObjText_SetText(objText, title_);	
	ObjText_SetFontSize(objText, 16);
	ObjText_SetFontBold(objText, true);
	ObjText_SetFontColorTop(objText, 255, 255, 0);
	ObjText_SetFontColorBottom(objText, 255, 64, 0);
	ObjText_SetFontBorderType(objText, BORDER_FULL);
	ObjText_SetFontBorderWidth(objText, 2);
	ObjText_SetFontBorderColor(objText, 64, 0, 64);
	ObjText_SetHorizontalAlignment(objText, ALIGNMENT_RIGHT);
	
	float x1 = GetStgFrameWidth() - 10;
	float y0 = GetStgFrameHeight() * 4 / 6;
	
	async
	{
		ascent(i in 0..30)
		{
			ObjRender_SetPosition(objText, Interpolate_Decelerate(0, x1, i/29), y0, 0);
			float scale = Interpolate_Decelerate(2, 1, i/29);
			ObjRender_SetScaleXYZ(objText, scale, scale, 1);
			yield;
		}
		wait(60);
		ascent(i in 0..30)
		{
			ObjRender_SetPosition(objText, x1, Interpolate_Accelerate(y0, 20, i/29), 0);
			yield;
		}
	}
	
	return objText;
}

func<int> CreateCutinExtra(int parent_, string text_, float dx_, float dy_)
{
	int obj = ObjText_Create();
	ObjText_SetText(obj, text_);	
	ObjText_SetFontSize(obj, 12);
	ObjText_SetFontColorTop(obj, 255, 255, 0);
	ObjText_SetFontColorBottom(obj, 255, 64, 0);
	ObjText_SetFontBorderType(obj, BORDER_FULL);
	ObjText_SetFontBorderWidth(obj, 2);
	ObjText_SetFontBorderColor(obj, 64, 0, 64);
	ObjText_SetHorizontalAlignment(obj, ALIGNMENT_CENTER);
	ObjRender_SetAlpha(obj, 128);
	
	async
	{
		while(!Obj_IsDeleted(obj) && !Obj_IsDeleted(parent_))
		{
			ObjRender_SetPosition(obj, ObjRender_GetX(parent_) - ObjText_GetTotalWidth(parent_)/2 + dx_, ObjRender_GetY(parent_) + dy_, 0);
			yield;
		}
		Obj_Delete(obj);
	}
	
	return obj;
}




func<int> BindCreateCutin(string title_, int objBound_)
{
	int objCutin = CreateCutin(title_);
	async
	{
		while(!Obj_IsDeleted(objBound_)) yield;
		Obj_Delete(objCutin);
	}
	return objCutin;
}

func<void> BindCutin(objCutin_, objParent_)
{
	async
	{
		while(!Obj_IsDeleted(objParent_)) yield;
		Obj_Delete(objCutin_);
	}
}