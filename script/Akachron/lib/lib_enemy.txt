#include "/script/Akachron/lib/lib_image.txt"

function CreateGenericAkashicDemon(let _isBoss)
{
	let enemyType;
	if(typeof(_isBoss) == VAR_BOOL) enemyType = _isBoss ? OBJ_ENEMY_BOSS : OBJ_ENEMY;
	else enemyType = _isBoss;
	let objEnemy = ObjEnemy_Create(enemyType);
	ObjEnemy_Regist(objEnemy);
	
	Obj_SetRenderPriorityI(objEnemy, PRI_ENEMY);
	
	let imgDemon = DIR_IMAGE ~ "/GenericAkashicDemon.png";
	ObjPrim_SetTexture(objEnemy, imgDemon);
	ObjSprite2D_SetSourceRect(objEnemy, 0, 0, 63, 63);
	ObjSprite2D_SetDestCenter(objEnemy);
	
	ObjEnemy_SetLife(objEnemy, 300);
	if(enemyType == OBJ_ENEMY) ObjEnemy_SetDamageRate(objEnemy, 1.0, 1.0);
	else ObjEnemy_SetDamageRate(objEnemy, 0.1, 0.1);
	
	async
	{
		int count = rand(0, 2047);
		yield; //give time to set anything manual the loop will need to take into account
		bool doScale = Obj_GetValueD(objEnemy, "DO_SCALE", true);
		
		while(!Obj_IsDeleted(objEnemy) && ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0)
		{
			count += rand(-2,4); //jittery movement that makes it slightly unsettling
			ObjRender_SetAngleZ(objEnemy, count*15);
			if(doScale) ObjRender_SetScaleXYZ(objEnemy, 1 + 0.1 * sin(count*30), 1 - 0.1 * cos(count*32), 1);
			
			ObjEnemy_SetIntersectionCircleToShot(objEnemy,   ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 32);
			ObjEnemy_SetIntersectionCircleToPlayer(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 24);
			
			yield;
		}
		//TODO: Little explosion effect if they died
		if(!Obj_IsDeleted(objEnemy))
		{
			_EffectEnemyBurst(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy));
			ExPlaySE(SE_SHOT_QUIET); //because i don't currently have any sound effects other than shot
			AddScore(1000);
		}
		Obj_Delete(objEnemy);
	}
	return objEnemy;
}

function CreateGenericBabelDemon(){
	//int obj = CreateGenericAkashicDemon(OBJ_ENEMY);
	int obj = CreateEnemy(OBJ_ENEMY);
	ObjRender_SetScaleXYZ(obj, 0.75, 0.75, 1);
	_FiddleWithDemon(obj, true);
	//Obj_SetValue(obj, "DO_SCALE", false);
	return obj;
}


func _FiddleWithDemon(int objEnemy_){ _FiddleWithDemon(objEnemy_, true); }
task _FiddleWithDemon(int objEnemy_, bool doScale_)
{
	int count = rand(0, 2047);
	bool doScale = Obj_GetValueD(objEnemy_, "DO_SCALE", true);
	float baseScaleX = ObjRender_GetScaleX(objEnemy_);
	float baseScaleY = ObjRender_GetScaleY(objEnemy_);
	
	while(!Obj_IsDeleted(objEnemy_) && ObjEnemy_GetInfo(objEnemy_, INFO_LIFE) > 0)
	{
		count += rand(-2,4); //jittery movement that makes it slightly unsettling
		ObjRender_SetAngleZ(objEnemy_, count*15);
		if(doScale) ObjRender_SetScaleXYZ(objEnemy_, baseScaleX + 0.1 * sin(count*30), baseScaleY - 0.1 * cos(count*32), 1);
		yield;
	}
}



//Adaptable to whatever the needs are
function CreateEnemy(){ return CreateEnemy(OBJ_ENEMY, true); }
function CreateEnemy(let type_){ return CreateEnemy(type_, true); }
function CreateEnemy(let type_, bool doIntersection_)
{
	int objEnemy = ObjEnemy_Create(type_);
	ObjEnemy_Regist(objEnemy);
	Obj_SetRenderPriorityI(objEnemy, PRI_ENEMY);
	let imgDemon = DIR_IMAGE ~ "/GenericAkashicDemon.png";
	ObjPrim_SetTexture(objEnemy, imgDemon);
	ObjSprite2D_SetSourceRect(objEnemy, 0, 0, 63, 63);
	ObjSprite2D_SetDestCenter(objEnemy);
	ObjEnemy_SetLife(objEnemy, 300);
	ObjEnemy_SetDamageRate(objEnemy, 1.0, 1.0);
	if(type_ == OBJ_ENEMY_BOSS) ObjEnemy_SetDamageRate(objEnemy, 0.1, 0.1);
	
	async
	{
		while(!Obj_IsDeleted(objEnemy) && ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0)
		{
			if(doIntersection_)
			{
				float scale = (ObjRender_GetScaleX(objEnemy) + ObjRender_GetScaleY(objEnemy)) / 2;
				ObjEnemy_SetIntersectionCircleToShot(objEnemy,   ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 32 * scale);
				ObjEnemy_SetIntersectionCircleToPlayer(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 24 * scale);
			}
			yield;
		}
		if(!Obj_IsDeleted(objEnemy))
		{
			_EffectEnemyBurst(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy));
			ExPlaySE(SE_SHOT_QUIET); //because i don't currently have any sound effects other than shot
			int pointVal = 10 * round(100 / ObjEnemy_GetInfo(objEnemy, INFO_DAMAGE_RATE_SHOT));
			AddScore(pointVal); //tankier enemies are worth more
			Obj_Delete(objEnemy);
		}
	}
	
	return objEnemy;
}


func<int[]> CreateDemonCloud(int enemyType_)
{
	int actualEnemyObj = CreateEnemy(enemyType_);
	
	Obj_SetVisible(actualEnemyObj, false);
	int[] fakes = [];
	float[] thetas = [];
	float[] radii  = [];
	float[] plungeMults = [];
	ascent(i in 0..15)
	{
		int obj = Create2DSprite(DIR_IMAGE ~ "/GenericAkashicDemon.png", 0, 0, 63, 63);
		fakes = fakes ~ [obj];
		thetas = thetas ~ [rand(0, 360)];
		radii = radii ~ [rand(5, 75)];
		plungeMults  = plungeMults ~ [rand(0, 3)];
	}
	async
	{
		int count = 0;
		while(!Obj_IsDeleted(actualEnemyObj) && ObjEnemy_GetInfo(actualEnemyObj, INFO_LIFE) > 0)
		{
			float ex = ObjRender_GetX(actualEnemyObj);
			float ey = ObjRender_GetY(actualEnemyObj);

			ObjEnemy_SetIntersectionCircleToShot(  actualEnemyObj, ex, ey, 64);
			ObjEnemy_SetIntersectionCircleToPlayer(actualEnemyObj, ex, ey, 48);
			
			for each(i, fake in fakes)
			{
				//float r = (50 - sqrt(i+1) * 15) * sin(count * sqrt(i + 1)) + rand(-5, 5) + 10 * i;
				//float th = 45 * i + 360 * sin(count * 1.7 * sqrt(i + 1)) + rand(-5, 5);
				thetas[i] = thetas[i] + rand(0, 5) * (-1.0f)^i;
				float r  = radii[i] * cos(count * plungeMults[i]);
				float th = thetas[i];
				
				ObjRender_SetPosition(fake, ex + r * cos(th), ey + r * sin(th), 0);
				ObjRender_SetAngleZ(fake, count * i + rand(-20, 20));
			}
			count++;
			yield;
		}
		for each(fake in fakes) 
		{
			Obj_Delete(fake);
		}
	}
	return [actualEnemyObj] ~ fakes;
}



func<int[]> CreateDemonCarrion()
{
	int actualEnemyObj  = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	int sprVultureBody  = Create2DSprite(DIR_IMAGE ~ "/DemonPlaceholderSheet.png",  64, 0,  64 + 63, 63);
	int sprCowSkull     = Create2DSprite(DIR_IMAGE ~ "/DemonPlaceholderSheet.png",   0, 0,       63, 63); //last so it's on top
	int[] allObjs = [ actualEnemyObj, sprCowSkull, sprVultureBody ];
	
	
	async
	{
		float ex; float ey;
		float bx; float by;
		int count = 0;
		float th;
		
		float dangleSpeed = 3;
		
		while(!Obj_IsDeleted(actualEnemyObj))
		{
			ex = ObjRender_GetX(actualEnemyObj);
			ey = ObjRender_GetY(actualEnemyObj);
			th = 90 + 30 * sin(count);
			bx = ex + 30 * cos(th);
			by = ey + 30 * sin(th);
			
			ObjRender_SetPosition(sprCowSkull, ex, ey, 0);
			ObjRender_SetAngleZ(sprCowSkull, 0);
			
			ObjRender_SetPosition(sprVultureBody,  bx, by, 0);
			ObjRender_SetAngleZ(sprVultureBody, th - 90);

			ObjEnemy_SetIntersectionCircleToShot(actualEnemyObj, ex, ey, 32);
			ObjEnemy_SetIntersectionCircleToPlayer(actualEnemyObj, ex, ey, 32);
			ObjEnemy_SetIntersectionCircleToPlayer(actualEnemyObj, bx, by, 32);
			
			int hits = ObjEnemy_GetInfo(actualEnemyObj, INFO_SHOT_HIT_COUNT);
			float enemySpeed = ObjMove_GetSpeed(actualEnemyObj) * Float_GetSign(ObjMove_GetSpeedX(actualEnemyObj));
			//dangleSpeed = clamp(dangleSpeed - 0.1 + 0.1 * enemySpeed + 0.1 * hits, 1, 5);
			//dangleSpeed = clamp(dangleSpeed - 0.2 + 0.5 * enemySpeed + 0.05 * hits, 0, 5);
			dangleSpeed = clamp(dangleSpeed * 0.99 + 2 * enemySpeed + 0.01 * hits * Float_GetSign(dangleSpeed), -5, 5);
			dangleSpeed += AngularDistance(th, 90) * 0.005;
			
			if(rand_int(0, 10) == 0) _Feather();
			
			count += dangleSpeed;
			yield;
		}
		Obj_Delete(sprCowSkull);
		Obj_Delete(sprVultureBody);
		
		task _Feather()
		{
			int sprFeather = Create2DSprite(DIR_IMAGE ~ "/DemonPlaceholderSheet.png", 128, 0, 128 + 63, 63);
			ObjRender_SetScaleXYZ(sprFeather, 0.25, 0.25, 1);
			float x0 = ex + rand(-30, 30);
			float y0 = ey + rand(-30, 30);
			float th0R = rand(0, 360);
			float dthR = rand(-1, 1);
			float thM = rand(0, 360);
			float vM = rand(0.1, 2);
			ascent(i in 0..60)
			{
				ObjRender_SetPosition(sprFeather, x0 + vM * i * cos(thM), y0 + vM * i * sin(thM), 0); 
				ObjRender_SetAngleZ(sprFeather, th0R + dthR * i);
				ObjRender_SetAlpha(sprFeather, 255 - i * 255 / 60);
				yield;
			}
		}
	}
	return allObjs;
}

func<int[]> CreateDemonInertia()
{
	int actualEnemyObj  = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	int sprWheel = Create2DSprite(DIR_IMAGE ~ "/DemonPlaceholderSheet.png",    0, 128,  63, 191);
	int sprHead = Create2DSprite(DIR_IMAGE ~ "/DemonPlaceholderSheet.png",    64, 128, 127, 191);
	int[] allObjs = [ actualEnemyObj, sprHead, sprWheel ];
	
	async
	{
		float count = 0;
		float dTh = 3;
		float thWheel = 0;
		float thHead = 0;
		float thTension = 0; //how wound the "neck" is - "deferred" rotation of the head
		bool unwinding = false;
		float ex; float ey;
		float scale = 0.5;
		
		while(!Obj_IsDeleted(actualEnemyObj))
		{
			ex = ObjRender_GetX(actualEnemyObj);
			ey = ObjRender_GetY(actualEnemyObj);
			
			ObjRender_SetPosition(sprWheel, ex, ey, 0);
			ObjRender_SetPosition(sprHead,  ex, ey, 0);
			
			thWheel += dTh;
			thTension += dTh;
			
			if(     !unwinding && (|thTension|)     > 720) unwinding = true;
			else if( unwinding && (|thTension|)%360 < 10)  unwinding = false; //why does it need such a large margin? 90, really?
			
			if(unwinding)
			{
				float thUnwind = 0.05 * thTension;
				
				if((|thTension|) < 30)
				{
					thUnwind = AngularDistance(thTension, 0) * 0.05;
					if((|thUnwind|) < 6) thUnwind = 6 * Float_GetSign(thUnwind);
				}
				
				thTension -= thUnwind;
				thHead += thUnwind;

				scale += (1 - scale) * 0.05;
			}
			else
			{
				thHead += AngularDistance(thHead, 0) * 0.05;	
				scale += (0.75 - scale) * 0.05;
			}
			
			ObjRender_SetAngleZ(sprWheel, thWheel);
			ObjRender_SetAngleZ(sprHead, thHead);
			ObjRender_SetScaleXYZ(sprHead, scale, scale, 1);
			
			ObjEnemy_SetIntersectionCircleToShot(actualEnemyObj, ex, ey, 32);
			ObjEnemy_SetIntersectionCircleToPlayer(actualEnemyObj, ex, ey, 24);
			
			count++;
			yield;
		}
		Obj_Delete(sprWheel);
		Obj_Delete(sprHead);
	}
	return allObjs;
}


func<int[]> CreateDemonDominion()
{
	int actualEnemyObj = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	int[] needles = [];
	int[] bouzus = [];
	int numNeedles = 8;
	int numBouzu   = 8;
	loop(numBouzu)   bouzus  = bouzus  ~ [ Create2DSprite(DIR_IMAGE ~ "/DemonPlaceholderSheet.png", 127, 192, 191, 255) ];
	loop(numNeedles) needles = needles ~ [ Create2DSprite(DIR_IMAGE ~ "/DemonPlaceholderSheet.png",  64, 192, 127, 255) ];
	int sprDoll = Create2DSprite(DIR_IMAGE ~ "/DemonPlaceholderSheet.png", 0, 192, 63, 255);
	
	int[] allObjs = [ actualEnemyObj, sprDoll ] ~ needles ~ bouzus;
	
	
	async
	{
		int count = 0;
		float ex; float ey;
		while(!Obj_IsDeleted(actualEnemyObj))
		{
			ex = ObjRender_GetX(actualEnemyObj);
			ey = ObjRender_GetY(actualEnemyObj);
			ObjRender_SetPosition(sprDoll, ex, ey, 0);

			ascent(i in 0..numNeedles)
			{
				float th = i * 360 / numNeedles;
				float r = 15 + 15 * cos(count * 3 + i * 720 / numNeedles);
				ObjRender_SetPosition(needles[i], ex + r * cos(th), ey + r * sin(th), 0);
				ObjRender_SetAngleZ(needles[i], th - 90);
				ObjRender_SetScaleXYZ(needles[i], 0.5, 0.75, 1);
			}
			
			ascent(i in 0..numBouzu)
			{
				float th = i * 360 / numBouzu + count;
				float r = 75 + 15 * cos(count * 1.5 + i * 720 / numBouzu);
				ObjRender_SetPosition(bouzus[i], ex + r * cos(th), ey + r * sin(th), 0);
				ObjRender_SetScaleXYZ(bouzus[i], 0.5, 0.5, 1);
			}
			
			ObjEnemy_SetIntersectionCircleToShot(actualEnemyObj, ex, ey, 32);
			ObjEnemy_SetIntersectionCircleToPlayer(actualEnemyObj, ex, ey, 32);
			
			count++;
			yield;
		}
		Obj_Delete(sprDoll);
		for each(needle in needles) Obj_Delete(needle);
		for each(bouzu in bouzus) Obj_Delete(bouzu);
	}
	
	return allObjs;
}